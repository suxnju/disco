{
	"Language": "Solidity",
	"Source": "pragma solidity ^0.4.11;\r\n\r\n\r\n/**\r\n * @title Ownable\r\n * @dev The Ownable contract has an owner address, and provides basic authorization control\r\n * functions, this simplifies the implementation of \"user permissions\".\r\n */\r\ncontract Ownable {\r\n    address public owner;\r\n\r\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\r\n    /**\r\n     * @dev The Ownable constructor sets the original `owner` of the contract to the sender\r\n     * account.\r\n     */\r\n    function Ownable() {\r\n        owner = msg.sender;\r\n    }\r\n\r\n    /**\r\n     * @dev Throws if called by any account other than the owner.\r\n     */\r\n    modifier onlyOwner() {\r\n        require(msg.sender == owner);\r\n        _;\r\n    }\r\n\r\n    /**\r\n     * @dev Allows the current owner to transfer control of the contract to a newOwner.\r\n     * @param newOwner The address to transfer ownership to.\r\n     */\r\n    function transferOwnership(address newOwner) onlyOwner public {\r\n        require(newOwner != address(0));\r\n        OwnershipTransferred(owner, newOwner);\r\n        owner = newOwner;\r\n    }\r\n\r\n}\r\n\r\n\r\n\r\n\r\n\r\n\r\n/**\r\n * @title SafeMath\r\n * @dev Math operations with safety checks that throw on error\r\n */\r\nlibrary SafeMath {\r\n    function mul(uint256 a, uint256 b) internal constant returns (uint256) {\r\n        uint256 c = a * b;\r\n        if (a != 0 && c / a != b) revert();\r\n        return c;\r\n    }\r\n\r\n    function div(uint256 a, uint256 b) internal constant returns (uint256) {\r\n        // assert(b > 0); // Solidity automatically throws when dividing by 0\r\n        uint256 c = a / b;\r\n        // assert(a == b * c + a % b); // There is no case in which this doesn't hold\r\n        return c;\r\n    }\r\n\r\n    function sub(uint256 a, uint256 b) internal constant returns (uint256) {\r\n        if (b > a) revert();\r\n        return a - b;\r\n    }\r\n\r\n    function add(uint256 a, uint256 b) internal constant returns (uint256) {\r\n        uint256 c = a + b;\r\n        if (c < a) revert();\r\n        return c;\r\n    }\r\n}\r\n\r\n\r\n\r\n/*\r\n * !!!IMPORTANT!!!\r\n * Based on Open Zeppelin Refund Vault contract\r\n * https://github.com/OpenZeppelin/zeppelin-solidity/blob/master/contracts/crowdsale/RefundVault.sol\r\n * the only thing that differs is a hardcoded wallet address\r\n */\r\n\r\n/**\r\n * @title RefundVault.\r\n * @dev This contract is used for storing funds while a crowdsale\r\n * is in progress. Supports refunding the money if crowdsale fails,\r\n * and forwarding it if crowdsale is successful.\r\n */\r\ncontract VLBRefundVault is Ownable {\r\n    using SafeMath for uint256;\r\n\r\n    enum State {Active, Refunding, Closed}\r\n    State public state;\r\n\r\n    mapping (address => uint256) public deposited;\r\n\r\n    address public constant wallet = 0x02D408bc203921646ECA69b555524DF3c7f3a8d7;\r\n\r\n    address crowdsaleContractAddress;\r\n\r\n    event Closed();\r\n    event RefundsEnabled();\r\n    event Refunded(address indexed beneficiary, uint256 weiAmount);\r\n\r\n    function VLBRefundVault() {\r\n        state = State.Active;\r\n    }\r\n\r\n    modifier onlyCrowdsaleContract() {\r\n        require(msg.sender == crowdsaleContractAddress);\r\n        _;\r\n    }\r\n\r\n    function setCrowdsaleAddress(address _crowdsaleAddress) external onlyOwner {\r\n        require(_crowdsaleAddress != address(0));\r\n        crowdsaleContractAddress = _crowdsaleAddress;\r\n    }\r\n\r\n    function deposit(address investor) onlyCrowdsaleContract external payable {\r\n        require(state == State.Active);\r\n        deposited[investor] = deposited[investor].add(msg.value);\r\n    }\r\n\r\n    function close(address _wingsWallet) onlyCrowdsaleContract external {\r\n        require(_wingsWallet != address(0));\r\n        require(state == State.Active);\r\n        state = State.Closed;\r\n        Closed();\r\n        uint256 wingsReward = this.balance.div(100);\r\n        _wingsWallet.transfer(wingsReward);\r\n        wallet.transfer(this.balance);\r\n    }\r\n\r\n    function enableRefunds() onlyCrowdsaleContract external {\r\n        require(state == State.Active);\r\n        state = State.Refunding;\r\n        RefundsEnabled();\r\n    }\r\n\r\n    function refund(address investor) public {\r\n        require(state == State.Refunding);\r\n        uint256 depositedValue = deposited[investor];\r\n        deposited[investor] = 0;\r\n        investor.transfer(depositedValue);\r\n        Refunded(investor, depositedValue);\r\n    }\r\n\r\n    /**\r\n     * @dev killer method that can bu used by owner to\r\n     *      kill the contract and send funds to owner\r\n     */\r\n    function kill() onlyOwner {\r\n        require(state == State.Closed);\r\n        selfdestruct(owner);\r\n    }\r\n}",
	"ContractName": "VLBRefundVault",
	"CompilerVersion": "v0.4.14+commit.c2215d46"
}